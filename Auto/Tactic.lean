import Lean
import Auto.Preprocessing
import Auto.Translation
import Auto.Solver.SMT
import Auto.HintDB
open Lean Elab Tactic

initialize
  registerTraceClass `auto.tactic
  registerTraceClass `auto.printLemmas
  registerTraceClass `auto.printProof

namespace Auto

-- **TODO**: Extend
syntax hintelem := term <|> "*"
syntax hints := ("[" hintelem,* "]")?
syntax autoinstr := ("üëç")?
syntax (name := auto) "auto" autoinstr hints : tactic

inductive Instruction where
  | none

def parseInstr : TSyntax ``Auto.autoinstr ‚Üí TacticM Instruction
| `(autoinstr|) => return .none
| `(autoinstr|üëç) => throwError "We appreciate your flatter üòé"
| _ => throwUnsupportedSyntax

inductive HintElem where
  -- A user-provided term
  | term     : Term ‚Üí HintElem
  -- Hint database, not yet supported
  | hintdb   : HintElem
  -- `*` adds all hypotheses in the local context
  -- Also, if `[..]` is not supplied to `auto`, all
  --   hypotheses in the local context are
  --   automatically collected.
  | lctxhyps : HintElem
deriving Inhabited, BEq

def parseHintElem : TSyntax ``hintelem ‚Üí TacticM HintElem
| `(hintelem| *)       => return .lctxhyps
| `(hintelem| $t:term) => return .term t
| _ => throwUnsupportedSyntax

structure InputHints where
  terms    : Array Term := #[]
  hintdbs  : Array Unit := #[]
  lctxhyps : Bool       := false
deriving Inhabited, BEq

-- Parse `hints` to an array of `Term`, which is still syntax
-- `Array Term`
def parseHints : TSyntax ``hints ‚Üí TacticM InputHints
| `(hints| [ $[$hs],* ]) => do
  let mut terms := #[]
  let mut lctxhyps := false
  let elems ‚Üê hs.mapM parseHintElem
  for elem in elems do
    match elem with
    | .term t => terms := terms.push t
    | .lctxhyps => lctxhyps := true
    | _ => throwError "parseHints :: Not implemented"
  return ‚ü®terms, #[], lctxhyps‚ü©
| `(hints| ) => return ‚ü®#[], #[], true‚ü©
| _ => throwUnsupportedSyntax

inductive Result where
  -- Unsatisfiable, witnessed by `e`
  | unsat : (e : Expr) ‚Üí Result
  -- Satisfiable, witnessed by an assignment to free variables
  | sat : (es : Array (FVarId √ó Expr)) ‚Üí Result
  -- Unknown
  | unknown : Result

instance : ToMessageData Result where
  toMessageData : Result ‚Üí MessageData
  | .unsat e => m!"Result.unsat {e}"
  | .sat es => .compose m!"Result.sat "
    (MessageData.array es (fun (id, e) => m!"{mkFVar id} := {e}"))
  | .unknown => m!"Result.unknown"

def collectLctxLemmas (lctxhyps : Bool) (ngoal : FVarId) : TacticM (Array Lemma) := do
  let fVarIds := (if lctxhyps then (‚Üê getLCtx).getFVarIds else #[ngoal])
  let mut lemmas := #[]
  for fVarId in fVarIds do
    let decl ‚Üê FVarId.getDecl fVarId
    if ¬¨ decl.isAuxDecl ‚àß (‚Üê Meta.isProp decl.type) then
      let declType ‚Üê Prep.preprocessTerm (‚Üê instantiateMVars decl.type)
      lemmas := lemmas.push ‚ü®mkFVar fVarId, declType, #[]‚ü©
  return lemmas

def collectUserLemmas (terms : Array Term) : TacticM (Array Lemma) := do
  let mut lemmas := #[]
  for lems in ‚Üê terms.mapM Prep.elabLemma do
    for ‚ü®proof, type, params‚ü© in lems do
      if ‚Üê Meta.isProp type then
        let type ‚Üê Prep.preprocessTerm (‚Üê instantiateMVars type)
        lemmas := lemmas.push ‚ü®proof, type, params‚ü©
      else
        -- **TODO**: Relax condition?
        throwError "invalid lemma {type} for auto, proposition expected"
  return lemmas

def traceLemmas (pre : String) (lemmas : Array Lemma) : TacticM Unit := do
  let mut cnt : Nat := 0
  let mut mdatas : Array MessageData := #[]
  for lem in lemmas do
    mdatas := mdatas.push m!"\n{cnt}: {lem}"
    cnt := cnt + 1
  trace[auto.printLemmas] mdatas.foldl MessageData.compose pre

-- `ngoal` means `negated goal`
def runAuto
  (instrstx : TSyntax ``autoinstr)
  (hintstx : TSyntax ``hints) (ngoal : FVarId) : TacticM Result := do
  let instr ‚Üê parseInstr instrstx
  let inputHints ‚Üê parseHints hintstx
  let lctxLemmas ‚Üê collectLctxLemmas inputHints.lctxhyps ngoal
  traceLemmas "Lemmas collected from local context:" lctxLemmas
  let userLemmas ‚Üê collectUserLemmas inputHints.terms
  traceLemmas "Lemmas collected from user-provided terms:" userLemmas
  let lemmas := lctxLemmas ++ userLemmas
  match instr with
  | .none =>
    -- Testing. Skipping universe level instantiation and monomorphization
    let afterReify (arr : Array Nat) : LamReif.ReifM Expr := (do
      let valids ‚Üê arr.mapM LamReif.lookupValidTable!
      let exportFacts := valids.map (¬∑.2)
      for (id, lams) in (‚Üê LamReif.getVarVal) do
        trace[auto.tactic] "FVar: {Expr.fvar id}, Œª Sort: {repr lams}"
      let proof ‚Üê Lam2D.callDuper exportFacts
      let proofLamTerm := exportFacts.foldr (fun t' t => t'.mkImp t) (.base .falseE)
      trace[auto.printProof] "Duper found proof {‚Üê instantiateMVars proof}"
      let imp ‚Üê LamReif.newAssertion proof (‚Üê LamReif.mkImportVersion proofLamTerm)
      let contra ‚Üê LamReif.impApps imp arr
      let checker ‚Üê LamReif.buildCheckerExpr contra
      let contra ‚Üê Meta.mkAppM ``Embedding.Lam.LamThmValid.getFalse #[checker]
      Meta.mkLetFVars ((‚Üê Reif.getFvarsToAbstract).map Expr.fvar) contra
      -- let commands := (‚Üê (lamFOL2SMT (‚Üê LamReif.getVarVal) exportFacts).run {}).1
      -- let _ ‚Üê liftM <| commands.mapM (fun c => IO.println s!"Command: {c}")
      -- Solver.SMT.querySolver commands
      )
    let afterMonomorphization : Reif.ReifM Expr := (do
      let ufacts ‚Üê liftM <| Reif.getFacts
      ((LamReif.uLiftAndReify ufacts afterReify).run' {}).run')
    let proof ‚Üê Monomorphization.collectPolyLog (fun hmap mfacts =>
      let hmaprev := hmap.toList.foldl (fun hm (key, val) => hm.insert val key) HashMap.empty
      -- Skipping monomorphization
      afterMonomorphization.run' { facts := mfacts, iPolyLog := hmaprev })
      (lemmas.map (fun x => (x.proof, x.type)))
    trace[auto.tactic] "Auto found proof of {‚Üê Meta.inferType proof}"
    return .unsat proof

@[tactic auto]
def evalAuto : Tactic
| `(auto | auto $instr $hints) => withMainContext do
  let startTime ‚Üê IO.monoMsNow
  -- Suppose the goal is `‚àÄ (x‚ÇÅ x‚ÇÇ ‚ãØ x‚Çô), G`
  -- First, apply `intros` to put `x‚ÇÅ x‚ÇÇ ‚ãØ x‚Çô` into the local context,
  --   now the goal is just `G`
  Elab.Tactic.evalTactic (‚Üê `(tactic| intros))
  let [nngoal] ‚Üê (‚Üê getMainGoal).apply (.const ``Classical.byContradiction [])
    | throwError "evalAuto :: Unexpected result after applying Classical.byContradiction"
  let (ngoal, absurd) ‚Üê MVarId.intro1 nngoal
  replaceMainGoal [absurd]
  withMainContext do
    let result ‚Üê runAuto instr hints ngoal
    match result with
    | Result.unsat e => do
      IO.println s!"Unsat. Time spent by auto : {(‚Üê IO.monoMsNow) - startTime}ms"
      absurd.assign e
    | Result.sat assig => throwError "Sat"
    | Result.unknown => throwError "Unknown"
| _ => throwUnsupportedSyntax

end Auto